import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'java'
  
  id 'org.springframework.boot' version '3.2.12'
  id 'io.spring.dependency-management' version '1.1.7'
  
  id 'io.freefair.lombok' version '8.4'
  id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
  id 'org.liquibase.gradle' version '2.2.0'
  
  id 'jacoco'
  id 'org.sonarqube' version '3.3'
  
  id 'com.heroku.sdk.heroku-gradle' version '2.0.0'
  id 'com.avast.gradle.docker-compose' version '0.17.12'

  id 'com.diffplug.spotless' version '7.2.0'

  id 'org.openrewrite.rewrite' version 'latest.release'
}

group = 'org.sitmun'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencyManagement {
  imports {
    mavenBom SpringBootPlugin.BOM_COORDINATES
  }
}

dependencies {
  // Data persistence and REST API
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  
  // Security and validation
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  
  // Monitoring and templating
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  
  // Development and testing database
  implementation "com.h2database:h2"
  
  // Production databases
  implementation 'org.postgresql:postgresql'
  implementation 'com.oracle.database.jdbc:ojdbc11-production'

  // Database migration
  implementation "org.liquibase:liquibase-core:${liquibase_version}"
  implementation "com.mattbertolini:liquibase-slf4j:${liquibase_slf4j_version}"
  
  // Query building
  implementation "com.querydsl:querydsl-jpa:${querydls_version}:jakarta"
  
  // Object mapping
  implementation "org.mapstruct:mapstruct:${mapstruct_version}"

  // Apache Commons utilities
  implementation "org.apache.commons:commons-lang3:${commons_lang3_version}"
  
  // Google Guava utilities
  implementation "com.google.guava:guava:${guava_version}"

  // OkHttp client with logging
  implementation platform("com.squareup.okhttp3:okhttp-bom:${okhttp_version}")
  implementation 'com.squareup.okhttp3:okhttp'
  implementation 'com.squareup.okhttp3:logging-interceptor'

  // JSON processing
  implementation "org.json:json:${json_version}"
  
  // JWT token handling
  implementation "io.jsonwebtoken:jjwt-api:${jjwt_version}"
  implementation "io.jsonwebtoken:jjwt-impl:${jjwt_version}"
  implementation "io.jsonwebtoken:jjwt-jackson:${jjwt_version}"

  // OpenAPI/Swagger documentation
  implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdoc_openapi_version}"

  // LDAP profile
  implementation 'org.springframework.ldap:spring-ldap-core'
  implementation 'org.springframework.security:spring-security-ldap'

  // Mail profile
  implementation 'org.springframework.boot:spring-boot-starter-mail'

  // Core annotation processing
  annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
  
  // QueryDSL code generation
  annotationProcessor "com.querydsl:querydsl-apt:${querydls_version}:jakarta"
  
  // MapStruct code generation
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"

  // Development-time annotations
  compileOnly "org.jetbrains:annotations:${jetbrains_annotations_version}"

  // Spring Boot test support
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
  }
  
  // Security testing
  testImplementation 'org.springframework.security:spring-security-test'
  
  // Testing utilities
  testImplementation 'org.hamcrest:hamcrest'
  
  // LDAP testing (embedded LDAP server)
  testImplementation 'com.unboundid:unboundid-ldapsdk'

  // Code evolution
  rewrite 'org.openrewrite.recipe:rewrite-spring:6.11.1'

}

// ============================================================================
// HEROKU DEPLOYMENT CONFIGURATION
// ============================================================================

// Stage task for Heroku deployment
tasks.register('stage') {
  group = 'Heroku'
  description = 'Prepare application for Heroku deployment'
  dependsOn ':processResources', ':bootJar'
  
  doLast {
    copy {
      from "build/libs/sitmun-backend-core-${version}.jar"
      into "."
      rename "sitmun-backend-core-${version}.jar", "sitmun.jar"
    }
  }
}

// Heroku configuration
heroku {
  appName = "sitmun-backend-core"
  includes = ["sitmun.jar"]
  includeBuildDir = false
  
  def procFile = [
    "web": "java -Dspring.profiles.active=heroku -Dserver.port=\$PORT \$JAVA_OPTS -cp sitmun.jar org.springframework.boot.loader.JarLauncher"
  ]
  processTypes(procFile)
}

// Deploy to Heroku
tasks.named("deployHeroku") {
  group = 'Heroku'
  description = 'Deploy the application to Heroku'
  dependsOn 'stage'
  
  doLast {
    delete "sitmun.jar"
  }
}

// Local Heroku testing
tasks.register('herokuDevLocal', JavaExec) {
  group = 'Heroku'
  description = 'Run the Heroku deployment locally'
  dependsOn 'stage'
  classpath = files("build/libs/sitmun-backend-core-${version}.jar")
}

// ============================================================================
// TESTING AND QUALITY ASSURANCE CONFIGURATION
// ============================================================================
// 
// Database Testing with Docker Compose Plugin
// ------------------------------------------
// The project uses the Gradle Docker Compose plugin for PostgreSQL testing.
// This provides automatic container lifecycle management with health checks.
// 
// Available tasks:
// - testPostgres: Run tests with PostgreSQL (automatically starts/stops container)
// - startPostgresTest: Start PostgreSQL container manually
// - stopPostgresTest: Stop PostgreSQL container manually  
// - cleanPostgresTest: Clean up containers and volumes
// 
// Configuration: docker/test-postgres-compose.yml
// ============================================================================

// Test configuration
tasks.withType(Test).configureEach {
  useJUnitPlatform()
  maxHeapSize = '2G'
  minHeapSize = '512m'

  // Reduce forking to improve performance - only fork when necessary
  forkEvery = 0
  
  // Memory settings for test JVM
  jvmArgs '-Xmx2g', '-Xms512m', '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=200', '-XX:+UseStringDeduplication'
  
  // Set system properties for test JVM
  systemProperty 'spring.profiles.active', 'test,h2'
  systemProperty 'java.awt.headless', 'true'
  
  // Test logging
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
}

// H2 Database Tests (default)
tasks.register('testH2', Test) {
  group = 'verification'
  description = 'Run tests with H2 in-memory database'
  
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
}

// PostgreSQL Database Tests
tasks.register('testPostgres', Test) {
  group = 'verification'
  description = 'Run tests with PostgreSQL database using Docker Compose'
  systemProperty 'spring.profiles.active', 'postgres'
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
  doFirst {
    sleep(5000) // Wait for Postgres container to be ready
  }
}

tasks.register('testOracle', Test) {
  group = 'verification'
  description = 'Run tests with OracleSQL database using Docker Compose'
  systemProperty 'spring.profiles.active', 'oracle'
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
  doFirst {
    sleep(10000) // Wait for Oracle container to be ready
  }
}

// Docker Compose configuration for PostgreSQL testing
dockerCompose {
  projectName = 'sitmun-backend-core'
  postgres {
    useComposeFiles = ['docker/postgres/test-docker-compose.yml']
    isRequiredBy(project.tasks.testPostgres)
  }
  oracle {
    useComposeFiles = ['docker/oracle/test-docker-compose.yml']
    isRequiredBy(project.tasks.testOracle)
  }
}

// All Database Tests
tasks.register('testAll', Test) {
  group = 'verification'
  description = 'Run tests with both H2 and PostgreSQL databases'
  
  dependsOn testH2, testPostgres
  
  // This task will run both test suites
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
}

// Code coverage reporting
jacocoTestReport {
  reports {
    xml.required.set(true)
    html.required.set(true)
  }
}

test.finalizedBy jacocoTestReport

// SonarQube integration
apply from: "$project.rootDir/sonar.gradle"

rootProject.tasks.sonarqube {
  dependsOn(tasks.jacocoTestReport)
}

// Spotless code formatting

spotless {
  java {
    removeUnusedImports()
    googleJavaFormat('1.28.0')
    // importOrder 'java', 'javax', 'org', 'com'
    target '**/*.java'
    targetExclude '**/generated-sources/**'
  }
}

// Code evolution

rewrite {
  activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_2")
  setExportDatatables(true)
}

